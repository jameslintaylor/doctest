(ns jameslintaylor.doctest.format
  "Docstring formatting utilities."
  (:require
   [cljfmt.main :as cljfmt]
   [clojure.string :as string]
   [clojure.test :as test]
   [jameslintaylor.doctest.parse :as parse]))

(def ^:private ^{:arglists '([fmt & args])} format-cljfmt
  (comp (partial #'cljfmt/reformat-string {}) format))

(defn wrap
  [n form]
  (with-meta form {::wrap n}))

(defmulti form-str
  "Given a form, return a formatted string suitable to be written to a
  clojure test file.

  If form is sequential, the :jameslintaylor.doctest.format/wrap
  metadata determines where to begin adding new lines.

  Usage:

  nil and literal strings are handled appropriately
  => (form-str '(foo nil \"bar\"))
  \"(foo nil \"bar\")\"

  elements after :jameslintaylor.doctest.format/wrap are on new lines
  => (form-str (wrap 2 '(foo bar baz)))
  \"(foo bar\n     baz)\""
  type)

(defn- seq-form-str [seq-form]
  (let [nwrap (::wrap (meta seq-form) (count seq-form))
        [h t] (split-at nwrap seq-form)]
    (format-cljfmt "(%s%s)"
                   (string/join " " (map form-str h))
                   (apply str (map (comp (partial str "\n") form-str) t)))))

(defmethod form-str clojure.lang.Cons
  [form]
  (seq-form-str form))

(defmethod form-str clojure.lang.IPersistentList
  [form]
  (seq-form-str form))

(defmethod form-str java.lang.String
  [form]
  (format "\"%s\"" form))

(defmethod form-str java.util.regex.Pattern
  [form]
  (format "#\"%s\"" form))

(defmethod form-str nil
  [_]
  "nil")

(defmethod form-str :default
  [form]
  (str form))

(defn assertion-form
  [assertion]
  (let [{:keys [expected expr msg]} assertion
        equality-form               (wrap 2 `(~'= ~expected ~expr))]
    (if msg
      (wrap 2 `(~'is ~equality-form ~msg))
      `(~'is ~equality-form))))

(defn var-test-form
  [var]
  (when-let [assertions (parse/doctest-assertions var)]
    (wrap 3 `(~'deftest ~(symbol "^:doctest") ~(symbol (str (-> var meta :name) "-test"))
              ~@(map assertion-form assertions)))))

(defn ns-form
  [ns]
  (wrap 2 `(~'ns ~(symbol (str ns "-doctest"))
            "Generated by doctest."
            ~(wrap 1 `(:require
                       ~(symbol (str ns))
                       [~'clojure.test :refer [~'deftest ~'is]])))))

(defn format-test-file
  [ns]
  (let [var-tests (into []
                        (comp (keep var-test-form)
                              (map form-str))
                        (vals (ns-interns ns)))]
    (format "%s%s"
            (form-str (ns-form ns))
            (apply str (map (partial str "\n\n") var-tests)))))
